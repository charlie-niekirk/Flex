plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-kapt'
    id 'com.google.android.libraries.mapsplatform.secrets-gradle-plugin'
    id 'dagger.hilt.android.plugin'
    id 'kotlin-parcelize'
    id 'androidx.navigation.safeargs'
    id 'com.google.gms.google-services'
    id 'com.google.firebase.crashlytics'
    id "com.google.protobuf" version "0.8.17"
}

android {
    compileSdk 31

    defaultConfig {
        applicationId "me.cniekirk.flex"
        minSdk 24
        targetSdk 31
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        coreLibraryDesugaringEnabled true
        // Sets Java compatibility to Java 8
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
        freeCompilerArgs += "-Xjvm-default=all"
    }
    buildFeatures {
        viewBinding true
    }
    hilt {
        enableExperimentalClasspathAggregation = true
    }
    lint {
        abortOnError false
    }
    configurations.all {
        exclude group: 'org.jetbrains', module: 'annotations-java5'
    }
}

kapt {
    correctErrorTypes true
}

dependencies {
    // Core dependencies
    implementation 'androidx.core:core-ktx:1.7.0'
    implementation 'androidx.appcompat:appcompat:1.4.1'
    implementation 'com.google.android.material:material:1.6.0-alpha02'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.3'
    implementation 'androidx.lifecycle:lifecycle-livedata-ktx:2.4.0'
    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.4.0'
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:2.4.0"
    implementation "androidx.datastore:datastore:1.0.0"
    implementation  "com.google.protobuf:protobuf-javalite:3.18.0"
    implementation "androidx.paging:paging-runtime-ktx:$paging_version"
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.5.2'
    implementation 'com.google.android.gms:play-services-maps:18.0.2'

    coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs:1.1.5'

    // DI dependencies
    implementation "com.google.dagger:hilt-android:$hilt_version"

    // Navigation dependencies
    implementation "androidx.navigation:navigation-fragment-ktx:$nav_version"
    implementation "androidx.navigation:navigation-ui-ktx:$nav_version"

    // Markdown rendering dependencies
    implementation "io.noties.markwon:core:$markwon_version"
    implementation "io.noties.markwon:ext-strikethrough:$markwon_version"
    implementation "io.noties.markwon:ext-tables:$markwon_version"
    implementation "io.noties.markwon:recycler-table:$markwon_version"
    implementation "io.noties.markwon:syntax-highlight:$markwon_version"
    implementation "io.noties.markwon:image-coil:$markwon_version"
    implementation "io.noties.markwon:linkify:$markwon_version"
    implementation "io.noties.markwon:editor:$markwon_version"
    implementation "io.noties:prism4j:$prism_version"

    // IO dependencies
    implementation "com.squareup.retrofit2:retrofit:$retrofit_version"
    implementation "com.squareup.retrofit2:converter-moshi:$retrofit_version"
    implementation "com.squareup.moshi:moshi:1.13.0"
    implementation 'com.squareup.moshi:moshi-adapters:1.13.0'
    implementation "androidx.room:room-runtime:$room_version"

    // Image loading dependencies
    implementation 'com.github.bumptech.glide:glide:4.12.0'
    implementation 'jp.wasabeef:glide-transformations:4.3.0'

    // Video dependencies
    implementation 'com.github.charlie-niekirk:toro:3.7.5'
    implementation 'com.google.android.exoplayer:exoplayer:2.16.1'
    implementation 'com.pierfrancescosoffritti.androidyoutubeplayer:core:11.0.1'

    // Logging dependencies
    implementation 'com.jakewharton.timber:timber:4.7.1'
    implementation "com.squareup.okhttp3:logging-interceptor:5.0.0-alpha.2"

    // Firebase dependencies
    implementation platform('com.google.firebase:firebase-bom:28.4.0')
    implementation 'com.google.firebase:firebase-analytics-ktx'
    implementation 'com.google.firebase:firebase-crashlytics-ktx'

    implementation "androidx.biometric:biometric-ktx:1.2.0-alpha04"

    // Annotation processors
    kapt 'com.github.bumptech.glide:compiler:4.12.0'
    kapt 'com.squareup.moshi:moshi-kotlin-codegen:1.13.0'
    kapt "androidx.room:room-compiler:$room_version"
    kapt "com.google.dagger:hilt-compiler:$hilt_version"
    kapt "io.noties:prism4j-bundler:$prism_version"

    // (Experimental) renderscript toolkit post Android 12
    implementation files('../libs/renderscript-toolkit-debug.aar')

    // Testing dependencies
    testImplementation 'junit:junit:4.13.2'
    testImplementation "org.mockito.kotlin:mockito-kotlin:2.2.11"
    testImplementation 'app.cash.turbine:turbine:0.6.1'
    testImplementation 'org.jetbrains.kotlinx:kotlinx-coroutines-test:1.5.1'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
    androidTestImplementation 'androidx.test.espresso:espresso-accessibility:3.4.0'
    androidTestImplementation 'com.google.dagger:hilt-android-testing:2.38.1'
    kaptAndroidTest 'com.google.dagger:hilt-android-compiler:2.38.1'
    debugImplementation "androidx.fragment:fragment-testing:1.4.1"
}

protobuf {
    protoc {
        artifact = "com.google.protobuf:protoc:3.18.0"
    }

    // Generates the java Protobuf-lite code for the Protobufs in this project. See
    // https://github.com/google/protobuf-gradle-plugin#customizing-protobuf-compilation
    // for more information.
    generateProtoTasks {
        all().each { task ->
            task.builtins {
                java {
                    option 'lite'
                }
            }
        }
    }
}
